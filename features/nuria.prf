include(nuriavars.prf)

# Add depend and include path..
DEPENDPATH      += $${NURIA_INSTALL_HEADERS}
INCLUDEPATH     += $${NURIA_INSTALL_HEADERS}

QMAKE_RPATHDIR  += $${NURIA_INSTALL_LIBS}
macx:LIBS       += -F$${NURIA_INSTALL_LIBS}
LIBS            += -L$${NURIA_INSTALL_LIBS}

# The NuriaFramework requires C++11
CONFIG += c++11

# Let tria run over all headers
contains(NURIA_MODULES, tria) {
  tria.output       = tria_${QMAKE_FILE_BASE}.cpp
  tria.variable_out = SOURCES
  tria.variables    = INCLUDEPATH TRIA_FLAGS
  tria.commands     = tria -o${QMAKE_FILE_OUT} $(TRIA_FLAGS) ${QMAKE_FILE_NAME} $(INCLUDEPATH)
  tria.input        = HEADERS
  QMAKE_EXTRA_COMPILERS += tria
}

# Modules
contains(NURIA, network) {
    contains(NURIA_MODULES, network):DEFINES += NURIA_NETWORK_LIB
    else:warning(NuriaFramework was built without Network)
    
    LIBS += -lNuriaNetworkQt$$QT_MAJOR_VERSION
    NURIA += core
    QT += network
    
}

contains(NURIA, core) {
    contains(NURIA_MODULES, core):DEFINES += NURIA_CORE_LIB
    else:warning(NuriaFramework was built without Core)
    
    LIBS += -lNuriaCoreQt$$QT_MAJOR_VERSION
    
}
